prototype(Sitegeist.Kaleidoscope:Source) < prototype(Neos.Fusion:Component) {

    @propTypes {
        imageSource = ${PropTypes.instanceOf('\\Sitegeist\\Kaleidoscope\\Domain\\ImageSourceInterface')}
    }

    imageSource = null
    srcset = null
    sizes = null
    width = null
    height = null
    format = null
    quality = null
    type = null
    media = null
    renderDimensionAttributes = true

    renderer = Neos.Fusion:Component {

        @context {
            imageSource = ${props.imageSource || __imageSource}
            format = ${props.format || __format}
            quality = ${props.quality || __quality}
            width = ${props.width || __width}
            height = ${props.height || __height}
            srcset = ${props.srcset || __srcset}
            sizes = ${props.sizes || __sizes}
        }

        @if.hasImageSource = ${imageSource && Type.instance(imageSource, '\\Sitegeist\\Kaleidoscope\\Domain\\ImageSourceInterface')}
        isScalableSource = ${imageSource && Type.instance(imageSource, '\\Sitegeist\\Kaleidoscope\\Domain\\ScalableImageSourceInterface')}

        imageSource = ${imageSource}
        imageSource.@process.applyDimensions = ${(props.width && props.height) ? value.withDimensions(props.width, props.height) : value}
        imageSource.@process.applyWidth = ${(props.width && !props.height) ? value.withWidth(props.width) : value}
        imageSource.@process.applyHeight = ${(props.height && !props.width) ? value.withHeight(props.height) : value}
        imageSource.@process.applyFormat = ${format ? value.withFormat(format) : value}
        imageSource.@process.applyQuality = ${quality ? value.withQuality(quality) : value}

        type = ${format ? 'image/' + format : props.type}
        srcset = ${srcset}
        sizes = ${sizes}
        media = ${props.media}
        renderDimensionAttributes = ${props.renderDimensionAttributes}

        renderer = afx`
            <source @if.has={props.imageSource}
                srcset={props.imageSource.srcset(props.srcset)}
                sizes={props.sizes}
                sizes.@if.isScalable={props.sizes && props.isScalableSource}
                sizes.@process.join={Type.isArray(value) ? Array.join(value, ', ') : value}
                type={props.type}
                media={props.media}
                width={props.renderDimensionAttributes ? props.imageSource.currentWidth : null}
                height={props.renderDimensionAttributes ? props.imageSource.currentHeight : null}
            />
        `
    }
}
